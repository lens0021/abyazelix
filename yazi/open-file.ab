import {contains} from "std/text"
import {dir_exist, file_append, file_exist, file_write} from "std/fs"
import {get_env_var} from "std/env"

fun log(log: Text): Null? {
    let path = get_env_var("HOME") + "/.config/abyazelix/debug.log"
    if file_exist(path): file_append(path, log)?
    else: file_write(path, log)?
}

fun zellij_current_tab(): Text? {
    let layout = $zellij action dump-layout$?
    // Find the focused tab
    let tab = $echo -e "{layout}" | grep -E '^    tab.+focus=true' -A 1000 | grep '^    }' --max-count 1 -B 1000$?
    // Remove indentation
    tab = $echo -e "{tab}" | cut -d' ' -f5-$?
    // log(tab)?

    return tab
}

fun zellij_write_chars(chars: Text): Null? {
    $zellij action write-chars "{chars}"$?
    $zellij action write 13$?
}

main(args) {
    let path = args[1]
    if path == "": fail 1
    log("Trying to open {path}")?

    let tab = zellij_current_tab()?
    $echo -e "{tab}" | grep hx >/dev/null$ failed {
        // The current pane is not running helix, so open helix in a new pane
        log("open")?
        $zellij action new-pane$?
        $sleep 0.4$?
        // Get the working directory
        let working_dir = dir_exist(path) then path else $dirname "{path}"$?
        zellij_write_chars("hx {path} -w {working_dir}")?
        fail 1
    }
    log("create")?
    // Move focus to the next pane
    $zellij action focus-next-pane$?
    // The current pane is running helix, use zellij actions to open the file
    $zellij action write 27$?
    zellij_write_chars(":open \"{path}\"")?
}


