import { replace_regex } from "std/text"
import { dir_exists, file_append, file_exists, file_write } from "std/fs"
import { env_var_get } from "std/env"

fun log(log: Text): Null {
    trust {
        let path = env_var_get("HOME") + "/.config/abyazelix/debug.log"
        if file_exists(path): file_append(path, log)
        else: file_write(path, log)
    }
}

fun zellij_current_tab(): Text? {
    const layout = $ zellij action dump-layout $?
    // Find the focused tab
    let tab = $ echo -e "{layout}" | grep -E '^    tab.+focus=true' -A 1000 | grep '^    }' --max-count 1 -B 1000 $?
    // Remove indentation
    tab = $ echo -e "{tab}" | cut -d' ' -f5- $?
    // log("current tab: {tab}")

    return tab
}

fun zellij_current_pane(): Text? {
    const tab = zellij_current_tab()?
    // Find the focused tab
    let pane = $ echo -e "{tab}" | grep -E '^(    )+pane.+focus=true' -A 1000 | grep -E '^(    )+}' --max-count 1 -B 1000 $?
    // Remove indentation
    pane = $ echo -e "{pane}" | cut -d' ' -f5- $?
    log("current pane: {pane}")

    return tab
}

fun zellij_write_chars(chars: Text): Null? {
    $ zellij action write-chars "{chars}" $?
    $ zellij action write 13 $?
}

fun open_new_helix(path: Text): Null? {
    // The current pane is not running helix, so open helix in a new pane
    $ zellij action new-pane $?
    $ sleep 0.4 $?
    // Get the working directory
    const working_dir = dir_exists(path) then path else $dirname "{path}"$?
    zellij_write_chars("cd {working_dir}; hx {path} -w {working_dir}")?
}

fun contains_regex(text: Text, phrase: Text, extended: Bool = false): Bool {
    const replaced = replace_regex(text, phrase, "", extended)
    return text != replaced
}

main(args) {
    let path = args[1]
    log("Trying to open {path}")
    if path == "": fail 1

    if contains_regex(zellij_current_tab()?, " command=\"([^\"]*hx\" ", true) {
        log("Helix is open in the current tab. Let's focus it")
        // Move focus to the next pane
        let limit = 3
        let cnt = 0
        loop {
            if cnt > limit {
                open_new_helix(path)?
                break
            }
            cnt += 1
            $ zellij action focus-next-pane $?
            if contains_regex(zellij_current_pane()?, " command=\"[^\"]*hx\" ", true) {
                // The current pane is running helix, use zellij actions to open the file
                $ zellij action write 27 $?
                zellij_write_chars(":open \"{path}\"")?
                break
            }
        }
    } else {
        log("Cannot find an open helix")
        open_new_helix(path)?
    }
}
